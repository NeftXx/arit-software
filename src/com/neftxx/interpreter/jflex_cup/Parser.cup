/*****************************************************************
 * Parser.java
 * 
 * Copyright ©2020 Ronald Berdúo. All Rights Reserved.
 * This software is the proprietary information of Ronald Berdúo.
 *
 *****************************************************************/
package com.neftxx.interpreter.jflex_cup;

import com.neftxx.interpreter.AritLanguage;
import com.neftxx.interpreter.ast.AstNode;
import com.neftxx.interpreter.ast.expression.assignment.Access;
import com.neftxx.interpreter.ast.expression.assignment.Assignment;
import com.neftxx.interpreter.ast.expression.Expression;
import com.neftxx.interpreter.ast.expression.assignment.StructureAssignment;
import com.neftxx.interpreter.ast.expression.function.DefaultArgument;
import com.neftxx.interpreter.ast.expression.function.FunctionCall;
import com.neftxx.interpreter.ast.expression.Identifier;
import com.neftxx.interpreter.ast.expression.Literal;
import com.neftxx.interpreter.ast.expression.operation.*;
import com.neftxx.interpreter.ast.expression.structure.StructureAccess;
import com.neftxx.interpreter.ast.statement.Block;
import com.neftxx.interpreter.ast.expression.function.FormalParameter;
import com.neftxx.interpreter.ast.expression.function.Function;
import com.neftxx.interpreter.ast.statement.Return;
import com.neftxx.interpreter.ast.type.TypeFacade;
import com.neftxx.util.NodeInfo;
import java_cup.runtime.ComplexSymbolFactory;
import java_cup.runtime.ComplexSymbolFactory.ComplexSymbol;
import java.util.ArrayList;
import java.util.LinkedList;
import java.util.List;

/**
 *
 * @author Ronald Berdúo
 */
parser code {:
    /**
     * Manejador de tipos
     */
    private static final TypeFacade TYPE_FACADE = TypeFacade.getInstance();
    
    /**
     * analizador
     */
    private AritLanguage aritLanguage;

    /**
     * Nombre del archivo que esta analizando
     */
    private String filename;

    /**
     * Constructor del analizador sintactico
     *
     * @param scann  Analizador lexico
     * @param sf     Fabrica de simbolos
     */
    public Parser(Lexer scann, ComplexSymbolFactory sf, AritLanguage aritLanguage) {
        super(scann, sf);
        this.aritLanguage = aritLanguage;
        this.filename = aritLanguage.filename;
    }

    /**
     *
     * Método al que se llama automaticamente ante algun error sintactico.
     *
     * @param s simbolo que provoco el error
     */
    @Override
    public void syntax_error(java_cup.runtime.Symbol s) {
        ComplexSymbol cs = (ComplexSymbol) s;
        error("Error: No se esperaba el siguiente simbolo " + cs.getName() + ".",
            cs.getLeft().getLine(), cs.getRight().getColumn());
    }

    /**
     *
     * Método al que se llama en el momento en que ya no es posible una
     * recuperacion de errores.

     * @param s simbolo que provoco el error
     * @throws Exception
     */
    @Override
    public void unrecovered_syntax_error(java_cup.runtime.Symbol s) throws Exception {
        ComplexSymbol cs = (ComplexSymbol) s;
        error("Error irrecuperable provocado simbolo " + cs.getName() + ".",
        cs.getLeft().getLine(), cs.getRight().getColumn());
    }

    /**
     * Guarda los errores en el manejador
     *
     * @param message mensaje de por que se provoco el error
     * @param line linea donde se encuentra el error
     * @param column columna donde se encuentra el error
     */
    private void error(String message, int line, int column) {
        List<Integer> ids = this.expected_token_ids();
        LinkedList<String> list = new LinkedList<>();
        for (Integer expected : ids) {
            list.add(this.symbl_name_from_id(expected));
        }
        if (list.isEmpty()) {
            aritLanguage.addSyntacticError(message, new NodeInfo(line, column, filename));
        } else {
            aritLanguage.addSyntacticError(message + "\nSi no que se esperaba alguno de los siguientes tokens: " + list,
                new NodeInfo(line, column, filename));
        }
    }
:};

/* Terminales (tokens devueltos por el escáner). */

/* Palabras claves */
terminal CONTINUE, FUNCTION, DEFAULT, RETURN, SWITCH, BREAK, WHILE, CASE, ELSE, FOR, IN, DO, IF;

/* Literales */
terminal Integer LIT_ENTERO;
terminal Double  LIT_DECIMAL;
terminal Boolean LIT_BOOLEANO;
terminal String  LIT_STRING;
terminal String  ID;
terminal String  NULL;

/* Separadores */
terminal LAMBDA, IGUAL_QUE, DIFERENTE_QUE, MAYOR_IGUAL_QUE, MENOR_IGUAL_QUE, MODULO,
MAS, MENOS, MULT, DIV, POTENCIA, IGUAL, MAYOR_QUE, MENOR_QUE, INTERROGANTE,
DOS_PUNTOS, OR, AND, NOT, PAR_IZQ, PAR_DER, COR_IZQ, COR_DER, PUNTO_COMA,
COMA, LLAVE_IZQ, LLAVE_DER, UMENOS;


/* No Terminales */
non terminal compilation_unit;
non terminal ArrayList<AstNode> global_statements;
non terminal AstNode global_statement;
non terminal Function function_declaration;
non terminal ArrayList<FormalParameter> formal_parameter_list;
non terminal FormalParameter formal_parameter;
non terminal Block block;
non terminal ArrayList<AstNode> block_statements;
non terminal AstNode statement;
non terminal Return return_statement;
non terminal Expression statement_expression;
non terminal Expression expression;
non terminal Expression var_assigment;
non terminal ArrayList<Access> access_list;
non terminal Access access;
non terminal StructureAccess structure_access;
non terminal Expression function_call;
non terminal ArrayList<Expression> argument_list;
non terminal Expression default_exp;

                                /* Precedencias */

/*Asociatividad */ /* Operador */                                           /* Nivel */
precedence right    IGUAL;                                                  // 1
precedence right    INTERROGANTE, DOS_PUNTOS;                               // 2
precedence left     OR;                                                     // 3
precedence left     AND;                                                    // 4
precedence left     DIFERENTE_QUE, IGUAL_QUE;                               // 5
precedence nonassoc MAYOR_IGUAL_QUE, MAYOR_QUE, MENOR_IGUAL_QUE, MENOR_QUE; // 6
precedence left     MAS, MENOS;                                             // 7
precedence left     MULT, DIV, MODULO;                                      // 8
precedence left     POTENCIA;                                               // 9
precedence right    UMENOS, NOT;                                            // 10;
precedence left     COR_IZQ, COR_DER, PAR_IZQ, PAR_DER;                     // 11;


/* Reglas gramaticales */
start with compilation_unit;


compilation_unit
    ::= global_statements:astNodes {:
        if (astNodes != null) aritLanguage.setAstNodes(astNodes);
    :}
;

global_statements
    ::= global_statements:statements global_statement:statement {:
            RESULT = statements;
            if (statement != null) RESULT.add(statement);
        :}
    |   global_statement:statement {:
            RESULT = new ArrayList<>();
            if (statement != null) RESULT.add(statement);
        :}
;

global_statement
    ::= statement:statement {: RESULT = statement; :}
    |   function_declaration:function {: RESULT = function; :}
    |   error PUNTO_COMA
;

function_declaration
    ::= ID:id IGUAL FUNCTION PAR_IZQ formal_parameter_list:parameters PAR_DER block:block {:
            NodeInfo info = new NodeInfo(idxleft.getLine(), idxright.getColumn(), filename);
            RESULT = new Function(info, id, parameters, block);
        :}
    |   ID:id IGUAL FUNCTION PAR_IZQ PAR_DER block:block {:
            NodeInfo info = new NodeInfo(idxleft.getLine(), idxright.getColumn(), filename);
            RESULT = new Function(info, id, block);
        :}
    |   ID:id IGUAL PAR_IZQ ID:idParameter COMA formal_parameter_list:parameters PAR_DER LAMBDA block:block {:
            NodeInfo info = new NodeInfo(idxleft.getLine(), idxright.getColumn(), filename);
            NodeInfo infoParameter = new NodeInfo(idParameterxleft.getLine(), idParameterxright.getColumn(), filename);
            parameters.add(0, new FormalParameter(infoParameter, idParameter));
            RESULT = new Function(info, id, parameters, block);
        :}
    |   ID:id IGUAL PAR_IZQ ID:idParameter IGUAL expression:expParameter COMA formal_parameter_list:parameters PAR_DER LAMBDA block:block {:
            NodeInfo info = new NodeInfo(idxleft.getLine(), idxright.getColumn(), filename);
            NodeInfo infoParameter = new NodeInfo(idParameterxleft.getLine(), idParameterxright.getColumn(), filename);
            parameters.add(0, new FormalParameter(infoParameter, idParameter, expParameter));
            RESULT = new Function(info, id, parameters, block);
        :}
    |   ID:id IGUAL PAR_IZQ ID:idParameter PAR_DER LAMBDA block:block {:
            NodeInfo info = new NodeInfo(idxleft.getLine(), idxright.getColumn(), filename);
            ArrayList<FormalParameter> parameters = new ArrayList<>();
            NodeInfo infoParameter = new NodeInfo(idParameterxleft.getLine(), idParameterxright.getColumn(), filename);
            parameters.add(new FormalParameter(infoParameter, idParameter));
            RESULT = new Function(info, id, parameters, block);
        :}
    |   ID:id IGUAL PAR_IZQ ID:idParameter IGUAL expression:expParameter PAR_DER LAMBDA block:block {:
            NodeInfo info = new NodeInfo(idxleft.getLine(), idxright.getColumn(), filename);
            ArrayList<FormalParameter> parameters = new ArrayList<>();
            NodeInfo infoParameter = new NodeInfo(idParameterxleft.getLine(), idParameterxright.getColumn(), filename);
            parameters.add(new FormalParameter(infoParameter, idParameter, expParameter));
            RESULT = new Function(info, id, parameters, block);
        :}
    |   ID:id IGUAL PAR_IZQ PAR_DER LAMBDA block:block {:
            NodeInfo info = new NodeInfo(idxleft.getLine(), idxright.getColumn(), filename);
            RESULT = new Function(info, id, block);
        :}
;

formal_parameter_list
    ::= formal_parameter_list:parameters COMA formal_parameter:parameter {: RESULT = parameters; RESULT.add(parameter); :}
    |   formal_parameter:parameter {: RESULT = new ArrayList<>(); RESULT.add(parameter); :}
;

formal_parameter
    ::= ID:id {:
            NodeInfo info = new NodeInfo(idxleft.getLine(), idxright.getColumn(), filename);
            RESULT = new FormalParameter(info, id);
        :}
    |   ID:id IGUAL expression:exp {:
            NodeInfo info = new NodeInfo(idxleft.getLine(), idxright.getColumn(), filename);
            RESULT = new FormalParameter(info, id, exp);
        :}
;

block
    ::= LLAVE_IZQ:llave LLAVE_DER {:
            NodeInfo info = new NodeInfo(llavexleft.getLine(), llavexright.getColumn(), filename);
            RESULT = new Block(info);
        :}
    |   LLAVE_IZQ:llave block_statements:statements LLAVE_DER {:
            NodeInfo info = new NodeInfo(llavexleft.getLine(), llavexright.getColumn(), filename);
            RESULT = new Block(info, statements);
        :}
;

block_statements
    ::= block_statements:statements statement:statement {:
            RESULT = statements;
            if (statement != null) RESULT.add(statement);
        :}
    |   statement:statement {:
            RESULT = new ArrayList<>();
            if (statement != null) RESULT.add(statement);
        :}
;

return_statement
    ::= RETURN:r PUNTO_COMA {:
            NodeInfo info = new NodeInfo(rxleft.getLine(), rxleft.getColumn(), filename);
            RESULT = new Return(info);
        :}
    |   RETURN:r expression:exp {:
            NodeInfo info = new NodeInfo(rxleft.getLine(), rxleft.getColumn(), filename);
            RESULT = new Return(info, exp);
        :}
    |   RETURN:r expression:exp PUNTO_COMA {:
            NodeInfo info = new NodeInfo(rxleft.getLine(), rxleft.getColumn(), filename);
            RESULT = new Return(info, exp);
        :}
;

statement
    ::= statement_expression:expression PUNTO_COMA {: RESULT = expression; :}
    |   statement_expression:expression {: RESULT = expression; :}
    |   return_statement:statement {: RESULT = statement; :}
;

statement_expression
    ::= var_assigment:assigment {: RESULT = assigment; :}
    |   function_call:functionCall {: RESULT = functionCall; :}
;

var_assigment
    ::= ID:id IGUAL expression:expression {:
            NodeInfo info = new NodeInfo(idxleft.getLine(), idxright.getColumn(), filename);
            RESULT = new Assignment(info, id, expression);
        :}
    |   ID:id access_list:list IGUAL expression:expression {:
            NodeInfo info = new NodeInfo(idxleft.getLine(), idxright.getColumn(), filename);
            RESULT = new StructureAssignment(info, id, list, expression);
        :}
;

access_list
    ::= access_list:list access:access {: RESULT = list; if (access != null) RESULT.add(access); :}
    |   access:access {: RESULT = new ArrayList<>(); if (access != null) RESULT.add(access); :}
;

access
    ::= COR_IZQ expression:exp1 COMA expression:exp2 COR_DER {:
            NodeInfo info = new NodeInfo(exp1xleft.getLine(), exp1xright.getColumn(), filename);
            RESULT = new Access(info, exp1, exp2);
        :}
    |   COR_IZQ expression:exp COMA COR_DER {:
            NodeInfo info = new NodeInfo(expxleft.getLine(), expxright.getColumn(), filename);
            RESULT = new Access(info, exp, Access.Type.TWO_MATRIX);
        :}
    |   COR_IZQ COMA expression:exp COR_DER {:
            NodeInfo info = new NodeInfo(expxleft.getLine(), expxright.getColumn(), filename);
            RESULT = new Access(info, exp, Access.Type.THREE_MATRIX);
        :}
    |   COR_IZQ expression:exp COR_DER {:
            NodeInfo info = new NodeInfo(expxleft.getLine(), expxright.getColumn(), filename);
            RESULT = new Access(info, exp, Access.Type.NORMAL);
        :}
    |   COR_IZQ COR_IZQ expression:exp COR_DER COR_DER {:
            NodeInfo info = new NodeInfo(expxleft.getLine(), expxright.getColumn(), filename);
            RESULT = new Access(info, exp, Access.Type.TWO_LIST);
        :}
;

function_call
    ::= ID:id PAR_IZQ PAR_DER {:
            NodeInfo info = new NodeInfo(idxleft.getLine(), idxright.getColumn(), filename);
            RESULT = new FunctionCall(info, id);
        :}
    |   ID:id PAR_IZQ argument_list:arguments PAR_DER {:
            NodeInfo info = new NodeInfo(idxleft.getLine(), idxright.getColumn(), filename);
            RESULT = new FunctionCall(info, id, arguments);
        :}
;

expression
    ::= function_call:expression {: RESULT = expression; :}
    |   expression:condition INTERROGANTE:op expression:exp1 DOS_PUNTOS expression:exp2 {:
            NodeInfo info = new NodeInfo(opxleft.getLine(), opxright.getColumn(), filename);
            RESULT = new Ternary(info, condition, exp1, exp2);
        :}
    |   expression:exp1 OR:op expression:exp2 {:
            NodeInfo info = new NodeInfo(opxleft.getLine(), opxright.getColumn(), filename);
            RESULT = new Logical(info, exp1, exp2, Logical.Operator.OR);
        :}
    |   expression:exp1 AND:op expression:exp2 {:
            NodeInfo info = new NodeInfo(opxleft.getLine(), opxright.getColumn(), filename);
            RESULT = new Logical(info, exp1, exp2, Logical.Operator.AND);
        :}
    |   expression:exp1 DIFERENTE_QUE:op expression:exp2 {:
            NodeInfo info = new NodeInfo(opxleft.getLine(), opxright.getColumn(), filename);
            RESULT = new Comparator(info, exp1, exp2, Comparator.Operator.UNEQUAL);
        :}
    |   expression:exp1 IGUAL_QUE:op expression:exp2 {:
            NodeInfo info = new NodeInfo(opxleft.getLine(), opxright.getColumn(), filename);
            RESULT = new Comparator(info, exp1, exp2, Comparator.Operator.EQUAL);
        :}
    |   expression:exp1 MAYOR_IGUAL_QUE:op expression:exp2 {:
            NodeInfo info = new NodeInfo(opxleft.getLine(), opxright.getColumn(), filename);
            RESULT = new Comparator(info, exp1, exp2, Comparator.Operator.GREATER_THAN_OR_EQUAL_TO);
        :}
    |   expression:exp1 MAYOR_QUE:op expression:exp2 {:
            NodeInfo info = new NodeInfo(opxleft.getLine(), opxright.getColumn(), filename);
            RESULT = new Comparator(info, exp1, exp2, Comparator.Operator.GREATER_THAN);
        :}
    |   expression:exp1 MENOR_IGUAL_QUE:op expression:exp2 {:
            NodeInfo info = new NodeInfo(opxleft.getLine(), opxright.getColumn(), filename);
            RESULT = new Comparator(info, exp1, exp2, Comparator.Operator.LESS_THAN_OR_EQUAL_TO);
        :}
    |   expression:exp1 MENOR_QUE:op expression:exp2 {:
            NodeInfo info = new NodeInfo(opxleft.getLine(), opxright.getColumn(), filename);
            RESULT = new Comparator(info, exp1, exp2, Comparator.Operator.LESS_THAN);
        :}
    |   expression:exp1 MAS:op expression:exp2 {:
            NodeInfo info = new NodeInfo(opxleft.getLine(), opxright.getColumn(), filename);
            RESULT = new Arithmetic(info, exp1, exp2, Arithmetic.Operator.SUM);
        :}
    |   expression:exp1 MENOS:op expression:exp2 {:
            NodeInfo info = new NodeInfo(opxleft.getLine(), opxright.getColumn(), filename);
            RESULT = new Arithmetic(info, exp1, exp2, Arithmetic.Operator.SUBTRACTION);
        :}
    |   expression:exp1 MULT:op expression:exp2 {:
            NodeInfo info = new NodeInfo(opxleft.getLine(), opxright.getColumn(), filename);
            RESULT = new Arithmetic(info, exp1, exp2, Arithmetic.Operator.MULTIPLICATION);
        :}
    |   expression:exp1 DIV:op expression:exp2 {:
            NodeInfo info = new NodeInfo(opxleft.getLine(), opxright.getColumn(), filename);
            RESULT = new Arithmetic(info, exp1, exp2, Arithmetic.Operator.DIVISION);
        :}
    |   expression:exp1 MODULO:op expression:exp2 {:
            NodeInfo info = new NodeInfo(opxleft.getLine(), opxright.getColumn(), filename);
            RESULT = new Arithmetic(info, exp1, exp2, Arithmetic.Operator.MODULE);
        :}
    |   expression:exp1 POTENCIA:op expression:exp2 {:
            NodeInfo info = new NodeInfo(opxleft.getLine(), opxright.getColumn(), filename);
            RESULT = new Arithmetic(info, exp1, exp2, Arithmetic.Operator.POWER);
        :}
    |   expression:exp access:access {:
            NodeInfo infoExp = new NodeInfo(expxleft.getLine(), expxright.getColumn(), filename);
            RESULT = new StructureAccess(infoExp, exp, access);
        :}
    |   MENOS:op expression:exp {:
            NodeInfo info = new NodeInfo(opxleft.getLine(), opxright.getColumn(), filename);
            RESULT = new UnarySubtraction(info, exp);
        :} %prec UMENOS
    |   NOT:op expression:exp {:
            NodeInfo info = new NodeInfo(opxleft.getLine(), opxright.getColumn(), filename);
            RESULT = new Not(info, exp);
        :} %prec NOT
    |   PAR_IZQ expression:exp PAR_DER {: RESULT = exp; :}
    |   ID:id {:
            NodeInfo info = new NodeInfo(idxleft.getLine(), idxright.getColumn(), filename);
            RESULT = new Identifier(info, id);
        :}
    |   LIT_ENTERO:lit {:
            NodeInfo info = new NodeInfo(litxleft.getLine(), litxright.getColumn(), filename);
            RESULT = new Literal(info, TYPE_FACADE.getIntegerType(), lit);
        :}
    |   LIT_DECIMAL:lit {:
            NodeInfo info = new NodeInfo(litxleft.getLine(), litxright.getColumn(), filename);
            RESULT = new Literal(info, TYPE_FACADE.getNumericType(), lit);
        :}
    |   LIT_BOOLEANO:lit {:
            NodeInfo info = new NodeInfo(litxleft.getLine(), litxright.getColumn(), filename);
            RESULT = new Literal(info, TYPE_FACADE.getBooleanType(), lit);
        :}
    |   LIT_STRING:lit {:
            NodeInfo info = new NodeInfo(litxleft.getLine(), litxright.getColumn(), filename);
            RESULT = new Literal(info, TYPE_FACADE.getStringType(), lit);
        :}
    |   NULL:lit {:
            NodeInfo info = new NodeInfo(litxleft.getLine(), litxright.getColumn(), filename);
            RESULT = new Literal(info, TYPE_FACADE.getStringType(), null);
        :}
;

argument_list
    ::= argument_list:arguments COMA expression:argument {: RESULT = arguments; RESULT.add(argument); :}
    |   argument_list:arguments COMA default_exp:argument {: RESULT = arguments; RESULT.add(argument); :}
    |   expression:argument {: RESULT = new ArrayList<>(); RESULT.add(argument); :}
    |   default_exp:argument {: RESULT = new ArrayList<>(); RESULT.add(argument);:}
;

default_exp
    ::= DEFAULT:defaultExp {:
            NodeInfo info = new NodeInfo(defaultExpxleft.getLine(), defaultExpxright.getColumn(), filename);
            RESULT = new DefaultArgument(info);
        :}
;
